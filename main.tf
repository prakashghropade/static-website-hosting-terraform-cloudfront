# aws s3 bucket
resource "aws_s3_bucket" "firstbucket" {
    bucket = var.bucket_name
}

# making the bucket private
resource "aws_s3_bucket_public_access_block" "s3_block" {
  bucket = aws_s3_bucket.firstbucket.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

# creating the origin access control for the coludfrom and bucket
resource "aws_cloudfront_origin_access_control" "oac" {
  name                              = "demo-oac"
  description                       = "Example Policy"
  origin_access_control_origin_type = "s3"
  signing_behavior                  = "always"
  signing_protocol                  = "sigv4"
}

# this are the bucket policies
resource "aws_s3_bucket_policy" "allow-cloudfront" {
  bucket = aws_s3_bucket.firstbucket.id
  depends_on = [ aws_s3_bucket_public_access_block.s3_block ]
  policy = jsonencode(
    {
  "Version": "2012-10-17",
  "Statement": [
    {
      "Sid": "AllowCluoudFront",
      "Effect": "Allow",
      "Principal": {
        Service = "cloudfront.amazonaws.com" 
      },
      "Action": [
        "s3:GetObject",
      ],
      "Resource":  "${aws_s3_bucket.firstbucket.arn}/*"
      Condition = {
        StringEquals = {
             "AWS:SourceArn" = aws_cloudfront_distribution.s3_distribution.arn
        }
      }
    }
  ]
}
  )
}

# upload the files on the s3 bucket
resource "aws_s3_object" "object" {
  for_each = fileset("${path.module}/www", "**/*")

  bucket = aws_s3_bucket.firstbucket.id
  key    = each.value
  source = "${path.module}/www/${each.value}"

  etag = filemd5("${path.module}/www/${each.value}")

  content_type = lookup({
    "html" = "text/html",
    "css"  = "text/css",
    "js"   = "application/javascript",
    "json" = "application/json",
    "png"  = "image/png",
    "jpg"  = "image/jpeg",
    "jpeg" = "image/jpeg",
    "gif"  = "image/gif",
    "svg"  = "image/svg+xml",
    "ico"  = "image/x-icon",
    "txt"  = "text/plain"
  }, split(".", each.value)[length(split(".", each.value)) - 1], "application/octet-stream")
}

# cloud front destribution
resource "aws_cloudfront_distribution" "s3_distribution" {
  origin {
    domain_name              = aws_s3_bucket.firstbucket.bucket_regional_domain_name
    
    origin_access_control_id = aws_cloudfront_origin_access_control.oac.id
    origin_id                = local.origin_id
  }

  enabled             = true
  is_ipv6_enabled     = true
  comment             = "Some comment"
  default_root_object = "index.html"


  default_cache_behavior {
    allowed_methods  = ["GET", "HEAD"]
    cached_methods   = ["GET", "HEAD"]
    target_origin_id = local.origin_id

    forwarded_values {
      query_string = false

      cookies {
        forward = "none"
      }
    }

    viewer_protocol_policy = "redirect-to-https"
    min_ttl                = 0
    default_ttl            = 3600
    max_ttl                = 86400
  }

  price_class = "PriceClass_100"

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    cloudfront_default_certificate = true
  }
}